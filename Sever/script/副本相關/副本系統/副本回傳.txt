-	script	InstanceRecaller	-1,{
ClearAllMemory:
	// xr候的副本
	InstanceRecaller_Id = 0;
	// xr候的副本地D名Q
	InstanceRecaller_Map$ = "";
	// xr候在地D上的 X 坐
	InstanceRecaller_X = 0;
	// xr候在地D上的 Y 坐
	InstanceRecaller_Y = 0;
	// xr的rg戳
	InstanceRecaller_OfflineTime = 0;
	// xr候的血量, 避免x回磲嶙突
	InstanceRecaller_LeftHP = false;
	// x的r候L是l
	InstanceRecaller_PartyLeader = 0;
	end;
OnPCLogoutEvent:
	if (getmapxy(.@map$,.@x,.@y,BL_PC) != 0) {
		goto ClearAllMemory;	// x取坐耸, 清除
	}
	
	if (!instance_id(IM_PARTY)) {
		goto ClearAllMemory;	// 不在副本e面, 清除
	}
	
	if (!compare(.@map$, "#")) {
		goto ClearAllMemory;	// 不在副本地D中, 清除
	}
	
	if (getcharid(1) == 0) {
		goto ClearAllMemory;	// 不在伍e面, 清除
	}
	
	// xr候的副本
	InstanceRecaller_Id = instance_id(IM_PARTY);
	// xr候的副本地D名Q
	InstanceRecaller_Map$ = .@map$;
	// xr候在地D上的 X 坐
	InstanceRecaller_X = .@x;
	// xr候在地D上的 Y 坐
	InstanceRecaller_Y = .@y;
	// xr的rg戳
	InstanceRecaller_OfflineTime = gettimetick(2);
	// xr候的血量, 避免x回磲嶙突
	InstanceRecaller_LeftHP = HP;
	// x的r候L是l
	InstanceRecaller_PartyLeader = getpartyleader(getcharid(1), 2);
	
	end;
OnPCLoginEvent:
	if (!instance_id(IM_PARTY)|| instance_id(IM_PARTY) != InstanceRecaller_Id || InstanceRecaller_Map$ == "" )
		end;
		
	sleep2 10000; // M入[蜥岬 10 秒再回

	if (!instance_id(IM_PARTY) || 
		instance_id(IM_PARTY) != InstanceRecaller_Id || 
		InstanceRecaller_Map$ == "" ) {
		goto ClearAllMemory;	// 不在副本e面, 清除
	}
	
	if (getcharid(1) == 0) {
		goto ClearAllMemory;	// 不在伍e面, 清除
	}
	
	if (InstanceRecaller_PartyLeader != getpartyleader(getcharid(1), 2)) {
		goto ClearAllMemory;	// L已Q人了, 清除
	}
	
	// 算玩家已x_游虻rg
	.@leave_secs = gettimetick(2) - InstanceRecaller_OfflineTime;
	
	// x多Lrg瓤梢宰踊氐礁北
	.@recall_secs = 60 * 5;
	
	if (.@leave_secs > .@recall_secs) {
		goto ClearAllMemory;	// 超^r限了, 清除
	}
	
	warp InstanceRecaller_Map$, InstanceRecaller_X, InstanceRecaller_Y;
	
	if (InstanceRecaller_LeftHP == 0) {
		// x的r候已死亡, 立刻他死掉
		percentheal -100, 0;
	}
	
	goto ClearAllMemory;	// 已完成自诱倩, 清除
	
	end;
}